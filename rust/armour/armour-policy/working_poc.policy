external logger @ "log_sock" {
  fn log(_) -> ()
  fn connection(str, ID, ID) -> ()
  fn tcp_connection(ID, ID) -> ()
}

// REST request allow/deny
fn require(req: HttpRequest, from: ID, to: ID) -> bool {
  logger::log("request", req);
  if let Some(date) = req.unique_header("date") {
     logger::connection(str::from_utf8(date), from, to)
  } else {
     logger::connection("-", from, to)
  };
  true
}

// TCP connection/deny
fn allow_connection(from: ID, to: ID) -> bool {
  logger::log("connection", from, to);
  true
}


fn test_connection(from: ID, to: ID) -> bool {
  let from_hosts = from.hosts();
  let to_hosts = to.hosts();

  let allow = ["debug", "launch"];
  let mongo = ["mongo-web-interface", "accounting"];
  let mysql_from = ["dbread", "dbwrite"];
  let mqtt = ["mqtt-trusted","mqtt-debug","mqtt-public"];
  let dtp = ["pulse", "notifications"] @ mongo @ mqtt;
  let untrusted = ["picolibri", "pipharm"];
  let verify = ["verify-id"];
  let mysql_to = ["mysql"] @ verify;
  let on_during = ["on-during-conversation"];
  let cloud = ["cloud-update"];

  if any to_host in to_hosts { to_host in mqtt } {
    any from_host in from_hosts { from_host in allow @ ["dtp"] @ mqtt @ untrusted @ verify @ on_during }
  } else if any to_host in to_hosts { to_host in dtp } {
    "dtp" in from_hosts
  } else if "mongo" in to_hosts {
    any from_host in from_hosts { from_host in mongo }
  } else if any to_host in to_hosts { to_host in mysql_to } {
    any from_host in from_hosts { from_host in mysql_from @ allow }
  } else if any to_host in to_hosts { to_host in ["dtp"] @ verify @ on_during @ mysql_from @ cloud} {
    any from_host in from_hosts { from_host in allow }
  } else { false}
}
//L7 policy, test for dtp container
//fn dtp_connection(req: HttpRequest, from: ID, to: ID) -> bool {

//  let to_hosts = to.hosts();
//  let mqtt = ["mqtt-trusted","mqtt-debug","mqtt-public"];
//  let dtp = ["pulse", "mongo-web-interface", "accounting", "notifications"] @ mqtt;

//  "dtp" in from.hosts() && any to_host in to_hosts { to_host in dtp }
//  &&
//  (req.method() == "POST" && req.path() == "poc/api/v1.0/notifications")
//}
